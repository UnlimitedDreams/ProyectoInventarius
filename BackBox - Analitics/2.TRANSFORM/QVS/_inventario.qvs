/*

Transformación: 	Entradas y Salidas
Descripcon: 		Información movimiento de la bodega.
ProductVersion:	1.0
Encargado: 		Miguel Angel Lemoz (Miglem0101@gmail.com)

				Historial de Cambios:
	Usuario		|		Fecha		|			Detalle		
-----------------------------------------------------------------------------
1	Miguel		|		28/06/2016	|	Inicio del proyecto
-----------------------------------------------------------------------------

*/
TRACE ;
TRACE *********************	Inventario 	************************;

EntradasSalidaTemp:
	LOAD
		IdProducto, 
		IdEntrada, 
		Upper(Comentario)														As Comentario, 
		Upper(Procedimiento)													As Procedimiento,	 
		Upper(Responsable)														As Responsable, 
		Date(FechaContable,'DD-MM-YYYY')										As FechaContable,
		Date#(Year(FechaContable)&Num(Month(FechaContable), '00'), 'YYYYMM') 	As PeriodoContable,
		Month(FechaContable) 													As MesContable, 
		Year(FechaContable)														As AñoContable;
	LOAD 
		IdProducto, 
		IdEntrada, 
		Comentario, 
		Procedimiento, 
		Responsable, 
		FechaContable
	FROM
	[$(vRutaOri)EntradaSalida.qvd]
	(qvd);

/*	Mapeo de usuarios por ID*/
Mapping
UsuariosTemp:
	LOAD 
	    User.Usuario 		As Responsable,
	    User.IdUsuario		As IdUsuario
	FROM
	[$(vRutaTra)_Usuarios.qvd]
	(qvd);



/*	Inicia transformación	*/
EntradasSalida:
	LOAD
		IdProducto, 
		IdEntrada, 
		ApplyMap('UsuariosTemp', Responsable, '9999999999') 	As IdUsuario,
		Comentario, 
		Procedimiento, 
		Responsable,
		FechaContable,
		PeriodoContable,
		MesContable, 
		AñoContable
	Resident EntradasSalidaTemp;
	DROP Table EntradasSalidaTemp;

/*	Fin transformación	*/
_Inventario:
	LOAD 
		IdProducto						As Inventario.IdProducto, 
		IdEntrada						As Inventario.IdEntrada, 
		IdUsuario						As Inventario.IdUsuario,

		Comentario						As Inventario.Comentario,
		Procedimiento					As Inventario.Procedimiento,
		Responsable						As Inventario.Responsable,
		
		FechaContable					As Inventario.FechaContable,
		PeriodoContable					As Inventario.PeriodoContable,
		MesContable						As Inventario.MesContable,
		AñoContable						As Inventario.AñoContable
	Resident EntradasSalida;
	DROP Table EntradasSalida;

Store _Inventario Into [$(vRutaTra)_Inventario.qvd];
DROP TABLE _Inventario;